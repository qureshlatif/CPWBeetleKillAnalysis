      model{
      
      # Need to supply:
        # Y, nPoint, gridID, nGrid
        # detection covariates: Time.b, DOY.b, ccov.b, shcov.b 
        # point-level abundance covariates: PctDead.b, YSO.b, Outbrk.b, TWIP.b
        # For point-level covariate imputation: PctDead.sd, PctDead.lower, TWIP.d, TWIP.sd,
        #    ccov.means, ccov.sd, shcov.means, shcov.sd, PctDead.b.missing, TWIP.b.missing, shcov.b.missing, ccov.b.missing
      
      # Prior on theta
      bt.0 ~ dnorm(0, 0.1)
      bt.Time ~ dnorm(0, 0.1)
      bt.Time2 ~ dnorm(0, 0.1)
      bt.DOY ~ dnorm(0, 0.1)
      bt.DOY2 ~ dnorm(0, 0.1)
      bt.ccov ~ dnorm(0, 0.1)
      bt.shcov ~ dnorm(0, 0.1)

      # Priors on psi and lambda
      beta0.mean ~ dnorm(0, 0.1)
      beta0.sd ~ dunif(0, 10)
      beta0.tau <- pow(beta0.sd, -2)
      for(g in 1:nGrid) {
        beta0[g] ~ dnorm(beta0.mean, beta0.tau)
      }

      bl.pdead ~ dnorm(0, 0.1)
      bl.pdead2 ~ dnorm(0, 0.1)
      bl.outbrk ~ dnorm(0, 0.1)
      bl.YSO ~ dnorm(0, 0.1)
      bl.YSO2 ~ dnorm(0, 0.1)
      bl.pdXYSO ~ dnorm(0, 0.1)
      bl.TWIP ~ dnorm(0, 0.1)

      # Impute missing data
      for(j in 1:nPoint) {
        PctDead.b.imp[j] ~ dnorm(PctDead.b[j], pow(PctDead.sd[j], -2))T(PctDead.lower,)
        TWIP.b.imp[j] ~ dnorm(TWIP.d[gridID[j]], pow(TWIP.sd[gridID[j]], -2))
        ccov.b.imp[j] ~ dnorm(ccov.means[gridID[j]], pow(ccov.sd[gridID[j]], -2))
        shcov.b.imp[j] ~ dnorm(shcov.means[gridID[j]], pow(shcov.sd[gridID[j]], -2))
      }

      ### Point-level loop
      for(j in 1:nPoint) {
        ## Abundance model
        log(lambda[j]) <- beta0[gridID[j]] + # Add point-level abundance covariates here
          bl.pdead*PctDead.b[j]*(1-PctDead.b.missing[j]) + bl.pdead*PctDead.b.imp[j]*PctDead.b.missing[j] + 
          bl.pdead2*pow(PctDead.b[j], 2)*(1-PctDead.b.missing[j]) +
          bl.pdead2*pow(PctDead.b.imp[j], 2)*PctDead.b.missing[j] +
          bl.outbrk*Outbrk.b[j] + bl.YSO*YSO.b[j] + bl.YSO2*pow(YSO.b[j], 2) +
          bl.pdXYSO*PctDead.b[j]*YSO.b[j]*(1-PctDead.b.missing[j]) +
          bl.pdXYSO*PctDead.b.imp[j]*YSO.b[j]*PctDead.b.missing[j] +
          bl.TWIP*TWIP.b[j]*(1-TWIP.b.missing[j]) + bl.TWIP*TWIP.b.imp[j]*TWIP.b.missing[j]
        N[j] ~ dpois(lambda[j])
        Y[j, 1:nInt] ~ dmulti(pic[j, 1:nInt], N[j]) # Observation model
        
        ## Detection parameters
        logit(p[j]) <- bt.0 + bt.Time*Time.b[j] + bt.Time2*pow(Time.b[j], 2) +  # Add detection covariates here.
          bt.DOY*DOY.b[j] + bt.DOY2*pow(DOY.b[j], 2) +
          bt.ccov*ccov.b[j]*(1-ccov.b.missing[j]) + bt.ccov*ccov.b.imp[j]*ccov.b.missing[j] +
          bt.shcov*shcov.b[j]*(1-shcov.b.missing[j]) + bt.shcov*shcov.b.imp[j]*shcov.b.missing[j]
        for(k in 1:nInt) {
          pi[j, k] <- p[j] * pow(1 - p[j], (k - 1))
          pic[j, k] <- pi[j, k] / pcap[j]
        }
        pcap[j] <- sum(pi[j, ])
      }

      ### Derived parameters
      N.mean <- sum(N[1:nPoint]) / nPoint
      p.mean <- sum(pcap[1:nPoint]) / nPoint
      }
